import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot'
    id 'com.google.protobuf'
    id 'java-library'
    id 'com.bmuschko.docker-remote-api' version '3.4.4'
}

dependencies {
    protobuf project(':kgis-datalake-idl')

    implementation "com.netifi:netifi-spring-boot-starter"
    compile 'io.dgraph:dgraph4j'

}

mainClassName = 'kgis.datalake.Application'
java {
    sourceCompatibility = JavaVersion.VERSION_13
    targetCompatibility = JavaVersion.VERSION_13
}

// Packaging
task buildImage(type: DockerBuildImage, dependsOn: build) {
    buildArgs = ['JAR_FILE': "${bootJar.archiveName}"]
    dockerFile = file("${projectDir}/Dockerfile")
    inputDir = file("${projectDir}")
    tags = ["gregnetifi/kgmovies-${projectDir.name}", "gregnetifi/kgmovies-${projectDir.name}:${version}"]
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc'
    }
    plugins {
        rsocketRpc {
            artifact = 'io.rsocket.rpc:rsocket-rpc-protobuf'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                rsocketRpc {}
            }
        }
    }
}


sourceSets {
    main {
        java {
            srcDirs = ['src/main/java','build/generated/source/proto/main/java','build/generated/source/proto/main/rsocketRpc']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}