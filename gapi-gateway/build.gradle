import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import io.github.kobylynskyi.graphql.codegen.gradle.GraphqlCodegenGradleTask

plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'com.google.protobuf'
    id 'com.bmuschko.docker-remote-api' version '3.4.4'
    id 'org.jetbrains.kotlin.jvm' version '1.3.60'
    id "io.github.kobylynskyi.graphql.codegen" version "1.3.0"
}

dependencies {
    protobuf project(':product-service-idl')
    protobuf project(':inventory-service-idl')

    implementation "org.mapstruct:mapstruct:1.3.0.Final"
    annotationProcessor "org.mapstruct:mapstruct-processor:1.3.0.Final"

    compileOnly "org.projectlombok:lombok:1.18.8"
    annotationProcessor "org.projectlombok:lombok:1.18.8"

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'com.netifi:netifi-spring-boot-starter'
    compile "com.graphql-java-kickstart:graphql-java-tools:5.7.1"
    runtime "com.graphql-java-kickstart:graphql-spring-boot-starter"
    compile "org.springframework.boot:spring-boot-starter-data-mongodb"
    compile "org.springframework.data:spring-data-commons"

    // to embed Altair tool
    runtime 'com.graphql-java-kickstart:altair-spring-boot-starter'
    // to embed GraphiQL tool
    runtime 'com.graphql-java-kickstart:graphiql-spring-boot-starter'
    // to embed Voyager tool
    runtime 'com.graphql-java-kickstart:voyager-spring-boot-starter'    
    runtime 'org.springframework.boot:spring-boot-starter-actuator'    
    
    // testing facilities
    testCompile 'com.graphql-java-kickstart:graphql-spring-boot-starter-test'
}

mainClassName = 'io.github.GraphqlApplication'

// Packaging
task buildImage(type: DockerBuildImage, dependsOn: build) {
    buildArgs = ['JAR_FILE': "${bootJar.archiveName}"]
    dockerFile = file("${projectDir}/Dockerfile")
    inputDir = file("${projectDir}")
    tags = ["gregnetifi/kgmovies-${projectDir.name}", "gregnetifi/kgmovies-${projectDir.name}:${version}"]
}


task graphqlCodegenZommon(type: GraphqlCodegenGradleTask) {
    graphqlSchemaPaths = ["$projectDir/src/main/resources/graphql/zommon-schema.graphqls".toString()]
    outputDir = new File("$buildDir/generated/graphql/zommon")
    modelPackageName = "io.github.conanchen.zommon.graphql.model"
    customTypesMapping = [
            DateTime: "java.util.Date"
    ]
    modelNameSuffix = "GQO"
    generateApis = false
}

task graphqlCodegenBikeshop(type: GraphqlCodegenGradleTask,dependsOn: graphqlCodegenZommon) {
    graphqlSchemaPaths = ["$projectDir/src/main/resources/graphql/bikeshop-schema.graphqls".toString()]
    outputDir = new File("$buildDir/generated/graphql/bikeshop")
    apiPackageName = "io.github.kobylynskyi.bikeshop.graphql.api"
    modelPackageName = "io.github.kobylynskyi.bikeshop.graphql.model"
    customTypesMapping = [
            DateTime: "java.util.Date"
    ]
    modelNameSuffix = "TO"
    generateApis = true
}

task graphqlCodegenMovie(type: GraphqlCodegenGradleTask,dependsOn: graphqlCodegenZommon) {
    graphqlSchemaPaths = ["$projectDir/src/main/resources/graphql/movies-schema.graphqls".toString()]
    outputDir = new File("$buildDir/generated/graphql/movies")
    apiPackageName = "io.github.conanchen.movies.graphql.api"
    modelPackageName = "io.github.conanchen.movies.graphql.model"
    customTypesMapping = [
            DateTime: "java.util.Date"
    ]
    modelNameSuffix = "GQO"
    generateApis = true
}

task graphqlCodegenMessage(type: GraphqlCodegenGradleTask,dependsOn: graphqlCodegenZommon) {
    graphqlSchemaPaths = ["$projectDir/src/main/resources/graphql/message-schema.graphqls".toString()]
    outputDir = new File("$buildDir/generated/graphql/message")
    apiPackageName = "io.github.conanchen.message.graphql.api"
    modelPackageName = "io.github.conanchen.message.graphql.model"
    customTypesMapping = [
            DateTime: "java.util.Date"
    ]
    modelNameSuffix = "GQO"
    generateApis = true
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc'
    }
    plugins {
        rsocketRpc {
            artifact = 'io.rsocket.rpc:rsocket-rpc-protobuf'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                rsocketRpc {}
            }
        }
    }
}


// Automatically generate GraphQL code on project build:
// compileJava.dependsOn 'graphqlCodegenBikeshop'
// compileJava.dependsOn 'graphqlCodegenBikeshop','graphqlCodegenMovie'

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java',
            'build/generated/source/proto/main/java',
            'build/generated/source/proto/main/rsocketRpc',
            'build/generated/sources/annotationProcessor/java/main'
            ]
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}